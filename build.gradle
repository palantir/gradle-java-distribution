buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://palantir.bintray.com/releases' }
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:0.3.18'
        classpath 'com.netflix.nebula:gradle-info-plugin:7.1.1'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.17.2'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.0.5'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.4.2'
        classpath 'de.undercouch:gradle-download-task:4.0.4'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

allprojects {
    apply plugin: 'com.palantir.java-format'
    version System.env.CIRCLE_TAG ?: gitVersion()
    group 'com.palantir.sls-packaging'

    repositories {
        jcenter()
        maven { url 'https://palantir.bintray.com/releases' }
    }
}

subprojects {
    apply plugin: 'java-library'
    java {
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror', '-Xlint:deprecation']
    }
}
